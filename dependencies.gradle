minecraft {
    mainLwjglVersion = 3
    lwjgl3Version = "3.3.3"
}
def asmVersion = '9.8'
def rfbVersion = '1.0.11'
// 100% binary compatible, 1 minor source compatibility issue (removed a throws IOException clause)
def gsonVersion = '2.10.1'

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute module('net.minecraft:launchwrapper') using module("com.gtnewhorizons.retrofuturabootstrap:RetroFuturaBootstrap:${rfbVersion}") because("LW replacement")
    }
}

def enable_custommainmenu = false
def enable_solarfluxreborn = false
def enable_logisticspipes = false
def enable_athenaeum = false
def enable_gaspunk = false
def enable_universaltweaks = false
def enable_ic2 = false
def enable_tconstruct = false
def enable_botania = false
def enable_gregtech = false



def modDeps = [
    'curse.maven:cmm-226406:2857869': enable_custommainmenu,
    'curse.maven:sfr-246974:3050838': enable_solarfluxreborn,
    'curse.maven:lp-232838:4849512': enable_logisticspipes,
    'curse.maven:athenaeum-284350:4633750': enable_athenaeum,
    'curse.maven:gaspunk-286505:3048663': enable_gaspunk,
    'com.github.Ladysnake:LadyLib:69e91b9': enable_gaspunk,
    'curse.maven:ut-705000:6959247': enable_universaltweaks,
    'curse.maven:ic2-242638:3838713' : enable_ic2,
    'curse.maven:gravisuite-253590:2700845' : enable_ic2,
    'curse.maven:tconstruct-74072:2902483' : enable_tconstruct,
    'curse.maven:plustic-376903:4703532': enable_tconstruct,
    'curse.maven:mantle-74924:2713386' : enable_tconstruct,
    'curse.maven:botania-225643:3330934' : enable_botania,
    'curse.maven:gregtech-557242:5519022' : enable_gregtech,

    // (Optional) Dependencies needed by other Mods, thus letting them always on
    'com.cleanroommc:configanytime:3.0': true,
    'curse.maven:baubles-227083:2518667': true,
    'curse.maven:codechickenlib-242818:2779848': true,
    'curse.maven:ctm-267602:2915363': true,

    // Cannot be used in runClient (Needs fixing in RFG)
    'curse.maven:EIO-64578:4674244': false,
    'curse.maven:EnderCore-231868:4671384': false
]

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Resolve newer versions of LaunchWrapper to allow java 9+ compat
    vanilla_minecraft("com.gtnewhorizons.retrofuturabootstrap:RetroFuturaBootstrap:${rfbVersion}") { transitive = false }
    forgePatchesEmbedded("com.gtnewhorizons.retrofuturabootstrap:RetroFuturaBootstrap:${rfbVersion}") { transitive = false }
    vanilla_minecraft("com.google.code.gson:gson:${gsonVersion}")
    forgePatchesEmbedded("com.google.code.gson:gson:${gsonVersion}")

    utilImplementation("org.ow2.asm:asm:${asmVersion}")
    utilImplementation("org.ow2.asm:asm-tree:${asmVersion}")
    utilImplementation("org.apache.commons:commons-lang3:3.14.0")
    utilImplementation("commons-io:commons-io:2.11.0")
    utilImplementation("commons-collections:commons-collections:3.2.2")
    utilImplementation("com.google.guava:guava:31.1-jre")

    hotswapCompileOnly("org.hotswapagent:hotswap-agent-core:1.4.1")
    hotswapCompileOnly("com.gtnewhorizons.retrofuturabootstrap:RetroFuturaBootstrap:${rfbVersion}")

    forgePatchesEmbeddedRecompile("org.ow2.asm:asm:${asmVersion}")
    forgePatchesEmbeddedRecompile("org.ow2.asm:asm-commons:${asmVersion}")
    forgePatchesEmbeddedRecompile("org.ow2.asm:asm-tree:${asmVersion}")
    forgePatchesEmbeddedRecompile("org.ow2.asm:asm-analysis:${asmVersion}")
    forgePatchesEmbeddedRecompile("org.ow2.asm:asm-util:${asmVersion}")

    forgePatchesEmbedded("org.apache.commons:commons-lang3:3.14.0")
    forgePatchesEmbedded("org.apache.commons:commons-compress:1.26.2")
    forgePatchesEmbedded("commons-io:commons-io:2.15.1")

    forgePatchesEmbedded("org.mvel:mvel2:2.5.2.Final")

    compileOnly("org.jetbrains:annotations:24.1.0")

    // Allow using Unsafe with newer javac versions
    compileOnly('me.eigenraven.java8unsupported:java-8-unsupported-shim:1.0.0')
    api("com.google.code.gson:gson:${gsonVersion}")

    forgePatchesEmbedded("jakarta.xml.bind:jakarta.xml.bind-api:3.0.1")
    forgePatchesEmbedded("javax.servlet:javax.servlet-api:4.0.1")
    forgePatchesEmbedded("com.sun.xml.bind:jaxb-impl:3.0.2")
    forgePatchesEmbedded("org.openjdk.nashorn:nashorn-core:15.4")

    //Needed by Fluidlogged API
    forgePatchesEmbedded("org.glassfish.corba:glassfish-corba-orb:5.0.0")
    forgePatchesEmbedded("jakarta.xml.ws:jakarta.xml.ws-api:4.0.2")

    //Needed by LP
    shadowImplementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10")

    shadowImplementation("com.github.weisj:darklaf-core:3.0.2")

    // Workaround for RFG issue
    patchedMinecraft("net.java.jinput:jinput:2.0.5")
    patchedMinecraft("net.java.jutils:jutils:1.0.0")

    modDeps.entrySet().forEach {
        if (it.value) {
            implementation rfg.deobf(it.key)
        } else {
            compileOnly rfg.deobf(it.key)
        }
    }
}

minecraft {
    groupsToExcludeFromAutoReobfMapping.add('curse.maven')
}

configurations.patchedMinecraft {
    exclude group: 'org.ow2.asm', module: 'asm-debug-all'
}
